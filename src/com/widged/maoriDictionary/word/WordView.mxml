<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark" 
		dataChange="onDataChange()" 
		title="Word Definition"
>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import com.widged.maoriDictionary.framework.FrameworkConstants;
			import com.widged.maoriDictionary.framework.events.FrameworkDispatcher;
			import com.widged.maoriDictionary.framework.events.FrameworkEvent;
			import com.widged.maoriDictionary.word.vo.WordItem;
			
			import spark.components.supportClasses.StyleableTextField;
			import spark.events.ViewNavigatorEvent;
			
			// central event bus
			private var frameworkDispatcher : FrameworkDispatcher;
			// class data
			private var dataChanged:Boolean = false;

			// #################################
			// ##   Events, Interaction
			// #################################
			
			private function onDefinitionAreaComplete() : void
			{
				StyleableTextField(definitionArea.textDisplay).styleSheet=FrameworkConstants.css;
				commitData();
			}
			
			private function onDataChange():void
			{
				dataChanged = true;
				commitData();
			}

			private function onClose():void
			{
				trace('[WordView.onClose]')
				frameworkDispatcher = FrameworkDispatcher.getInstance();
				frameworkDispatcher.dispatchEvent(new FrameworkEvent(FrameworkEvent.WORD_CLOSE));
			}
			
			// #################################
			// ##  View
			// #################################
			/**
			 * Commit any change to the word item data. 
			 * 
			 * Update any view element that displays word item information.  
			 */
			/* :NOTE: invalidate/commit is a way to achieve binding but in a more lightweight fashion */
			private function commitData():void
			{
				// wait until all components are on stage
				if(!wordLabel || !definitionArea) { return; }
				// don't change the view if the data haven't changed
				if(!dataChanged) {return;}
				// refresh
				var item:WordItem    = data as WordItem;
				wordLabel.text       = item.displayName;
				StyleableTextField(definitionArea.textDisplay).htmlText = item.toHtml(item.definition);
			}
			
		]]>
	</fx:Script>
	<s:Label 
		id="wordLabel"       
		left="28" right="24"
		fontSize="16"
		top="40"  
		creationComplete="commitData()"
	/>
	<s:TextArea 
		id="definitionArea"
		left="24" right="24"
		top="72" bottom="102"  
		creationComplete="onDefinitionAreaComplete();" 
		borderVisible="false" 
		fontSize="11" color="#808080" 
		verticalAlign="top" 
		editable="false" selectable="true" 
		>
	</s:TextArea>
	<s:Button 
		label="close" 
		horizontalCenter="0"
		bottom="24"
		click="onClose()" />
</s:View>
